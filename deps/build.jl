using Libdl

function find_lib_ker()
    if haskey(ENV,"JULIA_MATHLINK") && haskey(ENV,"JULIA_MATHKERNEL")
        return ENV["JULIA_MATHLINK"], ENV["JULIA_MATHKERNEL"]
    elseif Sys.isapple()
        # we query OS X metadata for possible non-default installations
        # TODO: can use `mdls -raw -name kMDItemVersion $path` to get the version
                
        # Mathematica
        for path in readlines(`mdfind "kMDItemCFBundleIdentifier == 'com.wolfram.Mathematica'"`)
            lib = joinpath(path,"Contents/Frameworks/mathlink.framework/mathlink")
            ker = joinpath(path,"Contents/MacOS/MathKernel")
            if isfile(lib) && isfile(ker)
                return lib, ker
            end
        end

        # Wolfram Engine
        for path in readlines(`mdfind "kMDItemCFBundleIdentifier == 'com.wolfram.engine'"`)
            # kernels are located in sub-application
            subpath = joinpath(path, "Contents/Resources/Wolfram Player.app")
            lib = joinpath(subpath,"Contents/Frameworks/mathlink.framework/mathlink")
            ker = joinpath(subpath,"Contents/MacOS/MathKernel")
            if isfile(lib) && isfile(ker)
                return lib, ker
            end
        end

    elseif Sys.isunix()
        archdir = Sys.ARCH == :arm ?    "Linux-ARM" :
                  Sys.ARCH == :x86_64 ? "Linux-x86-64" :
                                        "Linux"

        ker = get(ENV,"JULIA_MATHKERNEL") do
            for kername in ["WolframKernel", "MathKernel", "wolfram", "math"]
                if Sys.isexecutable(Sys.which(kername))
                    return kername
                end
            end

            for mpath in ["/usr/local/Wolfram/Mathematica","/opt/Wolfram/WolframEngine"]
                if isdir(mpath)
                    vers = readdir(mpath)
                    ver = vers[argmax(map(VersionNumber,vers))]
                    for kername in ["WolframKernel", "MathKernel", "wolfram", "math"]
                        fullkername = joinpath(mpath,ver,"Executables",kername)
                        if Sys.isexecutable(fullkername)
                            return fullkername
                        end
                    end
                end
            end
            error("Could not find Wolfram engine kernel")
        end

        basepath = String(read(pipeline(`printf "WriteString[\$Output,\$InstallationDirectory]"`, `$ker -noprompt`)))

        lib = Libdl.find_library(
            ["libML$(Sys.WORD_SIZE)i4","libML$(Sys.WORD_SIZE)i3"],
            [joinpath(basepath,"SystemFiles/Links/MathLink/DeveloperKit",archdir,"CompilerAdditions")])

        return lib, ker
    elseif Sys.iswindows()
        archdir = Sys.ARCH == :x86_64 ? "Windows-x86-64" :
            "Windows"

        #TODO: query Windows Registry, see RCall.jl
        mpath = "C:\\Program Files\\Wolfram Research\\Mathematica"
        if isdir(mpath)
            vers = readdir(mpath)
            ver = vers[argmax(map(VersionNumber,vers))]
            lib = Libdl.find_library(
                ["libML$(Sys.WORD_SIZE)i4","libML$(Sys.WORD_SIZE)i3"],
                [joinpath(mpath,ver,"SystemFiles\\Links\\MathLink\\DeveloperKit",archdir,"SystemAdditions")])
            ker = joinpath(mpath,ver,"math.exe")
            return lib, ker
        end
    end

    error("Could not find Mathematica or Wolfram Engine installation.\nPlease set the `JULIA_MATHLINK` and `JULIA_MATHKERNEL` variables.")
end

mlib,mker = find_lib_ker()

open("deps.jl","w") do f
    println(f, "# this file is automatically generated")
    println(f, :(const mlib = $mlib))
    println(f, :(const mker = $mker))
end
